licenses(["notice"])

load("//third_party/drake_rules:pybind.bzl", "pybind_library")

filegroup(
    name = "srcs",
    srcs = glob([
        "*.hpp",
        "opt/*.hpp",
        "src/*.cpp",
    ]) + ["BUILD.bazel"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "ctest_srcs",
    srcs = glob([
        "test/*.hpp",
        "test/*.cpp",
    ]),
)

filegroup(
    name = "ptest_srcs",
    srcs = glob([
        "test/*.py",
    ]),
)

######### LIBRARY #########

genrule(
    name = "generated_llo",
    srcs = ["cfg/llo.json"],
    outs = [
        "generated/api.hpp",
        "generated/api.cpp",
        "generated/codes.hpp",
        "generated/codes.cpp",
        "generated/grader.hpp",
        "generated/grader.cpp",
        "generated/opmap.hpp",
        "generated/opmap.cpp",
        "generated/pyapi.cpp",
    ],
    tools = ["//pybinder:pyagen"],
    cmd = "$(location //pybinder:pyagen) " +
        "--cfg $(location :cfg/llo.json) " +
        "--out $(@D)/generated " +
        "--strip_prefix=$$(dirname $(@D))",
)

cc_library(
    name = "llo",
    hdrs = glob([
        "*.hpp",
        "opt/*.hpp",
    ]) + [
        ":generated/api.hpp",
        ":generated/codes.hpp",
        ":generated/grader.hpp",
        ":generated/opmap.hpp",
    ],
    srcs = glob(["src/*.cpp"]) + [
        ":generated/api.cpp",
        ":generated/codes.cpp",
        ":generated/grader.cpp",
        ":generated/opmap.cpp",
    ],
    copts = ["-std=c++14"],
    deps = [
        "//opt:opt",
        "//coord:coord",
        "@com_github_mingkaic_tenncor//bwd:bwd",
    ],
    visibility = ["//visibility:public"],
)

pybind_library(
    name = "age_py",
    cc_srcs = [":generated/pyapi.cpp"],
    cc_deps = [":llo"],
)

pybind_library(
    name = "llo_py",
    cc_srcs = [":python/llo.cpp"],
    cc_deps = [
        ":llo",
        "@com_github_mingkaic_tenncor//dbg:dbg",
    ],
    py_deps = [":age_py"],
    visibility = ["//visibility:public"],
)

######### TEST #########

cc_test(
    name = "ctest",
    srcs = [":ctest_srcs"],
    copts = ["-std=c++14"],
    deps = [
        ":llo",
        "@gtest//:gtest",
        "@com_github_mingkaic_tenncor//dbg:dbg",
    ],
    linkstatic = True,
)

py_test(
    name = "ptest",
    srcs = [":ptest_srcs"],
    deps = [":llo_py"],
)
